//pref
specular|float|0.0|0.2|1
overlayVolume|set|2
edgeThresh|float|0|0.5|1
edgeAlpha|float|0|0.8|1
boundThresh|float|0.0|0.3|0.95
edgeBoundMix|float|0|0.85|1
colorTemp|float|0.0|1|1
overAlpha|float|0.0|1.0|2
overDistance|float|0.0|0.7|1
//frag
uniform int loops, overlays;
uniform float  specular,edgeAlpha, overDistance, overAlpha,edgeBoundMix,edgeThresh,   colorTemp, clipPlaneDepth,boundThresh, stepSize, sliceSize, viewWidth, viewHeight;
uniform sampler3D gradientVol;
uniform sampler3D overlayVol;
uniform sampler3D overlayGradientVol;
uniform sampler2D backFace;
uniform vec3 clearColor, clipPlane, lightPosition;
uniform bool showGrayscale;
#ifdef CUBIC_FILTER
uniform vec3 textureSize;

vec4 texture3Df(sampler3D vol, vec3 coord) {
/* License applicable to this function:
Copyright (c) 2008-2013, Danny Ruijters. All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:
*  Redistributions of source code must retain the above copyright
   notice, this list of conditions and the following disclaimer.
*  Redistributions in binary form must reproduce the above copyright
   notice, this list of conditions and the following disclaimer in the
   documentation and/or other materials provided with the distribution.
*  Neither the name of the copyright holders nor the names of its
   contributors may be used to endorse or promote products derived from
   this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
POSSIBILITY OF SUCH DAMAGE.

The views and conclusions contained in the software and documentation are
those of the authors and should not be interpreted as representing official
policies, either expressed or implied.

When using this code in a scientific project, please cite one or all of the
following papers:
*  Daniel Ruijters and Philippe ThÃ©venaz,
   GPU Prefilter for Accurate Cubic B-Spline Interpolation,
   The Computer Journal, vol. 55, no. 1, pp. 15-20, January 2012.
*  Daniel Ruijters, Bart M. ter Haar Romeny, and Paul Suetens,
   Efficient GPU-Based Texture Interpolation using Uniform B-Splines,
   Journal of Graphics Tools, vol. 13, no. 4, pp. 61-69, 2008.
*/
  // shift the coordinate from [0,1] to [-0.5, textureSize-0.5]
  //vec3 textureSize = uResolution; //textureSize3D(tex, 0));
  //vec3 textureSize = textureSize3D(vol, 0);

  vec3 coord_grid = coord * textureSize - 0.5;
  vec3 index = floor(coord_grid);
  vec3 fraction = coord_grid - index;
  vec3 one_frac = 1.0 - fraction;

  vec3 w0 = 1.0/6.0 * one_frac*one_frac*one_frac;
  vec3 w1 = 2.0/3.0 - 0.5 * fraction*fraction*(2.0-fraction);
  vec3 w2 = 2.0/3.0 - 0.5 * one_frac*one_frac*(2.0-one_frac);
  vec3 w3 = 1.0/6.0 * fraction*fraction*fraction;

  vec3 g0 = w0 + w1;
  vec3 g1 = w2 + w3;
  vec3 mult = 1.0 / textureSize;
  vec3 h0 = mult * ((w1 / g0) - 0.5 + index);  //h0 = w1/g0 - 1, move from [-0.5, textureSize-0.5] to [0,1]
  vec3 h1 = mult * ((w3 / g1) + 1.5 + index);  //h1 = w3/g1 + 1, move from [-0.5, textureSize-0.5] to [0,1]

  // fetch the eight linear interpolations
  // weighting and fetching is interleaved for performance and stability reasons
  vec4 tex000 =  texture3D(vol,h0);
  vec4 tex100 =  texture3D(vol,vec3(h1.x, h0.y, h0.z));
  tex000 = mix(tex100, tex000, g0.x);  //weigh along the x-direction
  vec4 tex010 =  texture3D(vol,vec3(h0.x, h1.y, h0.z));
  vec4 tex110 =  texture3D(vol,vec3(h1.x, h1.y, h0.z));
  tex010 = mix(tex110, tex010, g0.x);  //weigh along the x-direction
  tex000 = mix(tex010, tex000, g0.y);  //weigh along the y-direction
  vec4 tex001 =  texture3D(vol,vec3(h0.x, h0.y, h1.z));
  vec4 tex101 =  texture3D(vol,vec3(h1.x, h0.y, h1.z));
  tex001 = mix(tex101, tex001, g0.x);  //weigh along the x-direction
  vec4 tex011 =  texture3D(vol,vec3(h0.x, h1.y, h1.z));
  vec4 tex111 =  texture3D(vol,h1);
  tex011 = mix(tex111, tex011, g0.x);  //weigh along the x-direction
  tex001 = mix(tex011, tex001, g0.y);  //weigh along the y-direction
  return mix(tex001, tex000, g0.z);  //weigh along the z-direction
}
#else //CUBIC_FILTER
vec4 texture3Df(sampler3D vol, vec3 coord) {
	return texture3D(vol, coord); //trilinear interpolation
}
#endif

void main() {
	float overAlphaFrac = overAlpha;
	if (overAlphaFrac > 1.0) overAlphaFrac = 1.0;
	float overLight = 1.0;
	if (overAlphaFrac > 1.0) overLight =  1.0 * ((overAlphaFrac - 1.5)/1.0);
	float edgeExp = 0.2;
	float boundExpD = 0.01;
	float shininess = 7.0;
	float edgeBoundMixD = pow(edgeBoundMix, 2.0);
	// get normalized pixel coordinate in view port (e.g. [0,1]x[0,1])
	vec2 pixelCoord = gl_FragCoord.st;
	pixelCoord.x /= viewWidth;
	pixelCoord.y /= viewHeight;
	// starting position of the ray is stored in the texture coordinate
	vec3 start = gl_TexCoord[1].xyz;
	vec3 backPosition = texture2D(backFace,pixelCoord).xyz;
	vec3 dir = backPosition - start;
	float len = length(dir);
	dir = normalize(dir);
	vec4 surfaceColor = vec4(1.0,1.0,1.0, 1.0);
	if (colorTemp < 0.5) {
		surfaceColor.b = 1.0-((0.5-colorTemp)*-0.5);
		surfaceColor.g = 1.0-((0.5-colorTemp)*-0.1);
	}
	if (colorTemp > 0.5) {
		surfaceColor.g = 1.0-((colorTemp-0.5)*-0.1);
		surfaceColor.r = 1.0-((colorTemp-0.5)*-0.5);
	}
	//next see if clip plane intersects ray
	float clipStart = 0.0;
	float clipEnd = len;
	if (clipPlaneDepth > -0.5) {
		gl_FragColor.rgb = vec3(1.0,0.0,0.0);
		//next, see if clip plane faces viewer
		bool frontface = (dot(dir , clipPlane) > 0.0);
		//next, distance from ray origin to clip plane
		float dis = dot(dir,clipPlane);
		if (dis != 0.0  )  dis = (-clipPlaneDepth - dot(clipPlane, start.xyz-0.5)) / dis;
		//we set "len = 0.0"  instead of "discard" or "return": return fails on 2006MacBookPro10.4ATI1900, discard fails on MacPro10.5NV8800 will not discard
		if (frontface) clipStart = dis;
		if (!frontface)  clipEnd = dis;
	}
	vec3 deltaDir = dir * stepSize;
	vec4 gradientSample, ocolorSample, colorSample,colAcc = vec4(0.0,0.0,0.0,0.0);
	vec4 prevNorm = vec4(0.0,0.0,0.0,0.0);
	vec4 oprevNorm = vec4(0.0,0.0,0.0,0.0);
	vec3 lightDirHeadOn =  normalize(gl_ModelViewMatrixInverse * vec4(0.0,0.0,1.0,0.0)).xyz ;
	float lengthAcc = 0.0;
	float opacityCorrection = stepSize/sliceSize;
	//We need to calculate the ray's starting position: We add a random fraction of the stepsize to the original starting point to dither the output
	vec3 samplePos = start.xyz + deltaDir* (fract(sin(gl_FragCoord.x * 12.9898 + gl_FragCoord.y * 78.233) * 43758.5453));
	vec4 edgeColor = vec4(0.0,0.0,0.0,1.0);
	vec4 overAcc = vec4(0.0,0.0,0.0,0.0);
	float boundAcc = 0.0;
	int overDepth = 0;
	int backDepthEnd, backDepthStart = loops;
	for(int i = 0; i < loops; i++) {
		float boundX = 0.0;
		gradientSample = texture3Df(gradientVol,samplePos);
		if ((gradientSample.a < 0.01) || (lengthAcc <= clipStart) || (lengthAcc > clipEnd)) {
			colorSample = vec4(0.0,0.0,0.0,0.0);
		} else {
			if (backDepthStart == loops) backDepthStart = i;
			backDepthEnd = i;
			if (gradientSample.a < prevNorm.a)
				gradientSample.rgb = prevNorm.rgb;
			prevNorm = gradientSample;
			colorSample = gradientSample;
			//gradientSample.rgb = normalize(gradientSample.rgb*2.0 - 1.0); //direction saved as 0..1, rescale to -1..1
			gradientSample.rgb = gradientSample.rgb*2.0 - 1.0; //do not normalize: vec3(0,0,0)!!
			if (gradientSample.a > boundThresh) {
				float lightNormDot = dot(gradientSample.rgb, lightDirHeadOn); //with respect to viewer
				float bound = colorSample.a * pow(1.0-abs(lightNormDot),6.0);
				bound = bound * pow(bound,boundExpD);
				bound = bound * (1.0 - overAcc.a);
				boundX = bound;
				bound = 1.0-pow((1.0 - bound), opacityCorrection);
				boundAcc= (1.0 - boundAcc) * bound + boundAcc;
			}
			colorSample.a = 0.0;
			colorSample.rgb = edgeColor.rgb * colorSample.a;
			if  (gradientSample.a > edgeThresh) {
				float edge = smoothstep(edgeThresh, 1.0, gradientSample.a);
				edge = pow(edge,edgeExp);
				float lightNormDot = dot(gradientSample.rgb, lightPosition); //with respect to light location
				if (lightNormDot > 0.0) {
					edge +=   specular * pow(max(dot(reflect(lightPosition, gradientSample.rgb), dir), 0.0), shininess);
					edge = min(edge, 1.0); //required on AMD
				}
				colorSample.rgb = surfaceColor.rgb * edge;
				colorSample.a = edge;
			}
		}

		if ( overlays > 0 ) {
			gradientSample= texture3Df(overlayGradientVol,samplePos); //interpolate gradient direction and magnitude
			if (gradientSample.a > 0.01) {
				if (gradientSample.a < oprevNorm.a)
					gradientSample.rgb = oprevNorm.rgb;
				oprevNorm = gradientSample;
				gradientSample.rgb = normalize(gradientSample.rgb*2.0 - 1.0); //direction saved as 0..1, rescale to -1..1
				ocolorSample = texture3Df(overlayVol,samplePos);
				ocolorSample.a *= gradientSample.a; //modulate by gradient
				ocolorSample.a *= overAlphaFrac;
				ocolorSample.a = sqrt(ocolorSample.a);
				//specular
				float lightNormDot = dot(gradientSample.rgb, lightPosition); //with respect to light location
				if (lightNormDot > 0.0)
					ocolorSample.rgb +=   overLight * specular * pow(max(dot(reflect(lightPosition, gradientSample.rgb), dir), 0.0), shininess);
				//float lightAmount = 0.1 * overLight;
				//diffuse
		  		//if (lightAmount > 0.0)
				//	ocolorSample.rgb += lightAmount*dot(normalize(lightPosition), gradientSample.rgb);
				if ( ocolorSample.a > 0.2) {
					if (overDepth == 0) overDepth = i;
					//colorSample.a = max(ocolorSample.a, colorSample.a);
					colorSample = ocolorSample;
					colorSample.rgb *= (1.0-boundX);
					//float ratio = colorSample.a/ocolorSample.a;
					//colorSample= max(ocolorSample, colorSample);
					//if (ratio < 1.0)
					//	colorSample= mix(colorSample, ocolorSample, colorSample.a/ocolorSample.a);
					//else
					//	colorSample = ocolorSample;

				}
				//accumulate overlay color
				ocolorSample.a = 1.0-pow((1.0 - ocolorSample.a), opacityCorrection);
				overAcc= (1.0 - overAcc.a) * ocolorSample + overAcc;
			}
		}

		colorSample.a = 1.0-pow((1.0 - colorSample.a), opacityCorrection);
		colorSample.rgb *= colorSample.a;
		//accumulate color
		colAcc= (1.0 - colAcc.a) * colorSample + colAcc;
		samplePos += deltaDir;
		lengthAcc += stepSize;
		if ( lengthAcc >= len )
			break;
	} //main loop
	colAcc.a *= edgeAlpha;
	if (boundAcc > 0.0) {
		if  ( ( edgeAlpha < 0.5 ) &&  ((clearColor.r+clearColor.g+clearColor.b) < 1.0) )
			colAcc.rgb = colAcc.rgb + (boundAcc* edgeBoundMix);
		else
			colAcc.rgb = colAcc.rgb - (boundAcc* edgeBoundMix);
			//colAcc.rgb = colAcc.rgb * (1.0 - (boundAcc* edgeBoundMix));
		colAcc.a = max(colAcc.a, boundAcc);
	}
	if ((overAcc.a > 0.0) && (overAlpha > 1.0))  {
		colAcc.a=max(colAcc.a,overAcc.a);
		if ((overDistance > 0.0) && (overDepth > backDepthStart) && (backDepthEnd > backDepthStart)) {
			if (overDepth > backDepthEnd) overDepth = backDepthStart; // backDepthEnd
			float dx = float(overDepth-backDepthStart)/ float(backDepthEnd - backDepthStart);
			dx = pow(1.0-dx, overDistance);
			dx = pow(dx, 2.0);
			overAcc *= dx;
		}
		overAlphaFrac = overAlpha - 1.0;
		colAcc.rgb=mix(colAcc.rgb, overAcc.rgb,  overAlphaFrac);
	}
	//float contrast = 1.5;
	//colAcc.rgb = (colAcc.rgb - 0.5) * contrast + 0.5;

	//if ((clearColor.r+clearColor.g+clearColor.b) > 1.5) colAcc.rgb = 1.0-colAcc.rgb;
	colAcc.rgb = mix(clearColor,colAcc.rgb,colAcc.a);
	if (len == 0.0) colAcc.rgb = clearColor;
	gl_FragColor = colAcc;
}